### Tridash Core Module
### Copyright (C) 2019  Alexander Gutev
###
### This program is free software: you can redistribute it and/or modify
### it under the terms of the GNU General Public License as published by
### the Free Software Foundation, either version 3 of the License, or
### (at your option) any later version.
###
### This program is distributed in the hope that it will be useful,
### but WITHOUT ANY WARRANTY; without even the implied warranty of
### MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
### GNU General Public License for more details.
###
### You should have received a copy of the GNU General Public License
### along with this program.  If not, see <http://www.gnu.org/licenses/>.
###
### Linking this library statically or dynamically with other modules is
### making a combined work based on this library. Thus, the terms and
### conditions of the GNU General Public License cover the whole
### combination.
###
### As a special exception, the copyright holders of this library give
### you permission to link this library with independent modules to
### produce an executable, regardless of the license terms of these
### independent modules, and to copy and distribute the resulting
### executable under terms of your choice, provided that you also meet,
### for each linked independent module, the terms and conditions of the
### license of that module. An independent module is a module which is
### not derived from or based on this library. If you modify this
### library, you may extend this exception to your version of the
### library, but you are not obliged to do so. If you do not wish to do
### so, delete this exception statement from your version.

### String Processing Functions

:module(core)

### Conversions

## Convert a string to a list of characters.
##
## string : The string
##
## Returns a list containing the characters of the string.

string->list(string) : {
    convert(string, pos) : {
        head <- string-at(string, pos)
        tail <- convert(string, pos + 1)

        head !- cons(head, tail) -> self @ make-list
        Empty! -> self @ make-list
    }

    convert(string, 0)
}


## Convert a list to a string.
##
## Each element of the list is first converted to a string by
## `string`, and then concatenated to the result string.
##
## list : The list to convert to a string.
##
## Returns a string containing the concatenation of each element of
## list, converted to a string.

list->string(list) : {
    convert(list, acc) : {
        list -> cons(head, tail)

        current <- string-concat(acc, head)
        rest <- convert(tail, current)

        list !- rest -> self @ convert
        acc -> self @ convert catch Empty
    }

    strings <- map(string, list)
    convert(strings, "")
}


### Utilities

## Create a formatted string, with placeholders in the string replaced
## by the arguments given in `args`.
##
## The resulting string contains the characters in `string` with each
## '%s' placeholder replaced by the corresponding argument in `args`,
## converted to a string. The first placeholder is replaced with the
## first argument in `args`, the second with the second argument and
## so on. A `%%` sequence is replaced with a literal '%'.
##
## string : The format string.
## args   : The arguments to be substituted in the string.
##
## Returns the string with the placeholders replaced.

format(string, ..(args)) : {
    format-chars(chars, args) : {
        chars -> list*(c(%), c(%) and char, rest-chars)
        chars -> list*(c(%), c(s), rest-chars) and substitute
        chars -> list*(char, rest-chars)

        args -> cons(arg, rest-args)

        substitute !- cons(arg, format-chars(rest-chars, rest-args)) -> self @ format
        char !- cons(char, format-chars(rest-chars, args)) -> self @ format

        Empty! -> self @ format
    }

    chars <- string->list(string)
    formatted <- format-chars(chars, args)

    list->string(formatted)
}


### Export Declarations

:export(string->list, list->string, format)
